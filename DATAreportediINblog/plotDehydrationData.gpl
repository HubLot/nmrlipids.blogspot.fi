set term pdfcairo enhanced rounded color dashed dashlength 1 size 10cm, 22cm #16.2cm
set out "dehydration.pdf"

set multiplot layout 7,1 tit " "
set tmargin 0
set bmargin 0
set rmargin 10.5
set lmargin 8.5
set xtics format " "
set mxtics 2
set ytics -2,0.04,2
set mytics 4
set ylabel "S_{CH}" font ",18" offset screen 0.01 #, graph 0.43
set label 141 at screen 0.09, graph 0.77 right font ",9" tc rgb "#00A368" # MacRog
set label 111 at screen 0.09, graph 0.67 right font ",9" tc rgb "#D70039" # Berger
set label 131 at screen 0.09, graph 0.57 right font ",9" tc rgb "#FFD300" # GAFF
set label 121 at screen 0.09, graph 0.47 right font ",9" tc rgb "#0088BF" # CHARMM36

mPOPC=760.076
mH2O=18.01528 

we=5   # linewidth experimental
re=0.7 # point radius experimental

w=2   # linewidth simulation
r=0.5 # point radius simulation

set style line  1 lt 1 pt 12 ps re lw we lc rgb "#000000" # for Ulrich (EXP) DOPC
set style line  2 lt 1 pt 8 ps re lw we lc rgb "#000000" # for Dvinskikh (EXP) DMPC
set style line  3 lt 1 pt 6 ps re lw we lc rgb "#000000" # for Bechinger (EXP) POPC

set style line 11 lt 2 pt 7 ps r lw w lc rgb "#D70039" # for Berger
set style line 12 lt 1 pt 7 ps r lw w lc rgb "#0088BF" # for CHARMM36
set style line 13 lt 2 pt 7 ps r lw w lc rgb "#FFD300" # for GAFF
set style line 14 lt 1 pt 7 ps r lw w lc rgb "#00A368" # for MacRog
set style line 15 lt 1 pt 11 ps r lw w lc rgb "#D70039" # for Berger DLPC


# Carbon: beta ####################################
set label 100 "{/Symbol b}" at graph 0.05,0.9 font ",18"
set key l at screen 0.025,0.08 Left reverse samplen 2 width 2 maxrows 5 font ",14"
# offsets:
d_MR=-0.06 # MacRog
d_Be=-0.045 # Berger
d_CH=0.0 # CHARMM36
d_GA=-0.07 # GAFF
# show just offsets:
set label 111 sprintf("S_{CH}%+g",d_Be) at graph 1.045, graph 0.5 center rotate by 90 font ",16" tc rgb "#D70039"
set label 141 sprintf("S_{CH}%+g",d_MR) at graph 1.11, graph 0.5 center rotate by 90 font ",16" tc rgb "#00A368"
set label 131 sprintf("S_{CH}%+g",d_GA) at graph 1.175, graph 0.5 center rotate by 90 font ",16" tc rgb "#FFD300"
set label 121 sprintf("S_{CH}%+g",d_CH) at graph 1.24, graph 0.5 center rotate by 90 font ",16" tc rgb "#0088BF"
	 
plot [60:0][:0.05]\
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::0::1 w l ls 11 notitle, \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::0::1 w l ls 13 notitle, \
    "./DLPC/dehydration/BERGER-300K.dat" u ($1):($2+d_Be) ev :::0::1 w lp ls 15 \
    t " DLPC 300K Berger", \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::0::1 w lp ls 11 \
    t " @{POPC}&{DLPC} 298K  &{Be}@{\"}&{rger}", \
    "./POPC/dehydration/MACROG-310K_blogged-29-12-14.dat" u ($1):($2+d_MR) ev :::0::1 w lp ls 14 \
    t " @{POPC}&{DLPC} 310K MacRog", \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::0::1 w lp ls 13 \
    t " {&{DL}}{@{\"}}&{PC} 303K GAFF", \
    "./POPC/dehydration/CHARMM36-303K.dat" u ($1):($2+d_CH) ev :::0::1 w lp ls 12 \
    t " {&{DL}}{@{\"}}&{PC} &{30}@{\"}&{3K} CHARMM36", \
    "./DOPC/dehydration/EXP-Ulrich1994.dat" u ($1):($2) every :::0::0 w lp ls 1 \
    t "@{DOPC}&{DMPC} 303K ^{_{^{2}}}H-NMR", \
    "./DMPC/dehydration/EXP-Dvinskikh2005b_COUPLINGS.dat" u ($1):($2) every :::0::0 w lp ls 2 \
    t "DMPC 314K ^{_{^{13}}}C-NMR", \
    "./POPC/dehydration/EXP-Bechinger1991.dat" u ((mPOPC*$1/100)/(mH2O*(1-$1/100))):($2) every :::0::0 w lp ls 3 \
    t "@{POPC}&{DMPC} 296K ^{_{^{2}}}H-NMR", \
    x w lp ls 0 ps 0 lc rgb "white" t " "

unset key

# Carbon: alpha ###################################
set label 100 "{/Symbol a}" at graph 0.05,0.89 font ",18"
# offsets:
d_MR=+0.02 #MacRog
d_Be=-0.065 # Berger
d_CH=-0.015 # CHARMM36
d_GA=0.0 # GAFF
# show just offsets:
set label 111 sprintf("S_{CH}%+g",d_Be)
set label 141 sprintf("S_{CH}%+g",d_MR)
set label 121 sprintf("S_{CH}%+g",d_CH)
set label 131 sprintf("S_{CH}%+g",d_GA)

plot [60:0][0.01:0.11] \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::2::3 w l ls 11 notitle, \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::2::3 w l ls 13 notitle, \
    "./DOPC/dehydration/EXP-Ulrich1994.dat" u ($1):($2) every :::1::1 w lp ls 1 t "Ulrich", \
    "./DMPC/dehydration/EXP-Dvinskikh2005b_COUPLINGS.dat" u ($1):($2) every :::1::1 w lp ls 2 t "Dvinskikh", \
    "./POPC/dehydration/EXP-Bechinger1991.dat" u ((mPOPC*$1/100)/(mH2O*(1-$1/100))):($2) every :::1::1 w lp ls 3 t "Bechinger", \
    "./POPC/dehydration/MACROG-310K_blogged-29-12-14.dat" u ($1):($2+d_MR) ev :::2::3 w lp ls 14 t "MacRog", \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::2::3 w lp ls 11 t "Berger", \
    "./DLPC/dehydration/BERGER-300K.dat" u ($1):($2+d_Be) ev :::2::3 w lp ls 15 t "Berger", \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::2::3 w lp ls 13 t "GAFF", \
    "./POPC/dehydration/CHARMM36-303K.dat" u ($1):($2+d_CH) ev :::2::3 w lp ls 12 t "CHARMM36"


# Carbon: g3 ######################################
# R and S together:
set label 100 "g_{3}" at graph 0.05,0.81 font ",18"
# offsets:
d_MR=-0.02 #MacRog
d_Be=0.07 # Berger
d_CH=0.0 # CHARMM36
d_GA=0.03 # GAFF
# show just offsets:
set label 111 sprintf("S_{CH}%+g",d_Be)
set label 141 sprintf("S_{CH}%+g",d_MR)
set label 121 sprintf("S_{CH}%+g",d_CH)
set label 131 sprintf("S_{CH}%+g",d_GA)



plot [60:0][-0.29:-0.08]\
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::4::5 w l ls 11 notitle, \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::4::5 w l ls 13 notitle, \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::4::5 w lp ls 11 t "Berger", \
    "./DLPC/dehydration/BERGER-300K.dat" u ($1):($2+d_Be) ev :::4::5 w lp ls 15 t "Berger", \
    "./POPC/dehydration/MACROG-310K_blogged-29-12-14.dat" u ($1):($2+d_MR) ev :::4::5 w lp ls 14 t "MacRog", \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::4::5 w lp ls 13 t "GAFF", \
    "./POPC/dehydration/CHARMM36-303K.dat" u ($1):($2+d_CH) ev :::4::5 w lp ls 12 t "CHARMM36", \
    "./DMPC/dehydration/EXP-Dvinskikh2005b_COUPLINGS.dat" u ($1):($2) every :::2::2 w lp ls 2 t "Dvinskikh"


# Carbon: g2 ######################################
set label 100 "g_{2}" at graph 0.05,0.9 font ",18"
# offsets:
d_MR=-0.0 #MacRog
d_Be=0.0 # Berger
d_CH=0.03 # CHARMM36
d_GA=0.065 # GAFF
# show just offsets:
set label 111 sprintf("S_{CH}%+g",d_Be)
set label 141 sprintf("S_{CH}%+g",d_MR)
set label 121 sprintf("S_{CH}%+g",d_CH)
set label 131 sprintf("S_{CH}%+g",d_GA)


plot [60:0][-0.215:-0.15]\
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::6::6 w l ls 11 notitle, \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::6::6 w l ls 13 notitle, \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::6::6 w lp ls 11 t "Berger", \
    "./DLPC/dehydration/BERGER-300K.dat" u ($1):($2+d_Be) ev :::6::6 w lp ls 15 t "Berger", \
    "./POPC/dehydration/MACROG-310K_blogged-29-12-14.dat" u ($1):($2+d_MR) ev :::6::6 w lp ls 14 t "MacRog", \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::6::6 w lp ls 13 t "GAFF", \
    "./POPC/dehydration/CHARMM36-303K.dat" u ($1):($2+d_CH) ev :::6::6 w lp ls 12 t "CHARMM36", \
    "./DMPC/dehydration/EXP-Dvinskikh2005b_COUPLINGS.dat" u ($1):($2) every :::3::3 w lp ls 2 t "Dvinskikh"

# Carbon: g1 ######################################
# R:
set label 100 "g_{1R}" at graph 0.05,0.92 font ",18"
# offsets:
d_MR=0.03 #MacRog
d_Be=-0.275 # Berger
d_CH=0.055 # CHARMM36
d_GA=0.035 # GAFF
# show just offsets:
set label 111 sprintf("S_{CH}%+g",d_Be)
set label 141 sprintf("S_{CH}%+g",d_MR)
set label 121 sprintf("S_{CH}%+g",d_CH)
set label 131 sprintf("S_{CH}%+g",d_GA)



plot [60:0][-0.165:-0.075]\
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::7::7 w l ls 11 notitle, \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::7::7 w l ls 13 notitle, \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::7::7 w lp ls 11 t "Berger", \
    "./DLPC/dehydration/BERGER-300K.dat" u ($1):($2+d_Be) ev :::7::7 w lp ls 15 t "Berger", \
    "./POPC/dehydration/MACROG-310K_blogged-29-12-14.dat" u ($1):($2+d_MR) ev :::7::7 w lp ls 14 t "MacRog", \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::7::7 w lp ls 13 t "GAFF", \
    "./POPC/dehydration/CHARMM36-303K.dat" u ($1):($2+d_CH) ev :::7::7 w lp ls 12 t "CHARMM36", \
    "./DMPC/dehydration/EXP-Dvinskikh2005b_COUPLINGS.dat" u ($1):($3) every :::4::4 w lp ls 2 t "Dvinskikh"

# S:
set label 100 "g_{1S}" at graph 0.05,0.85 font ",18"
set xtics format "%g"
set xlabel "n_{w/l}" font ",18" 
# offsets:
d_MR=-0.045 #MacRog
d_Be=-0.09 # Berger
d_CH=0.0 # CHARMM36
d_GA=-0.085 # GAFF
# show just offsets:
set label 111 sprintf("S_{CH}%+g",d_Be)
set label 141 sprintf("S_{CH}%+g",d_MR)
set label 121 sprintf("S_{CH}%+g",d_CH)
set label 131 sprintf("S_{CH}%+g",d_GA)



plot [60:0][-0.07:] \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::8::8 w l ls 11 notitle, \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::8::8 w l ls 13 notitle, \
    "./POPC/dehydration/BERGER-298K.dat" u ($1):($2+d_Be) ev :::8::8 w lp ls 11 t "Berger", \
    "./DLPC/dehydration/BERGER-300K.dat" u ($1):($2+d_Be) ev :::8::8 w lp ls 15 t "Berger", \
    "./POPC/dehydration/MACROG-310K_blogged-29-12-14.dat" u ($1):($2+d_MR) ev :::8::8 w lp ls 14 t "MacRog", \
    "./POPC/dehydration/GAFF-303K.dat" u ($1):($2+d_GA) ev :::8::8 w lp ls 13 t "GAFF", \
    "./POPC/dehydration/CHARMM36-303K.dat" u ($1):($2+d_CH) ev :::8::8 w lp ls 12 t "CHARMM36", \
    "./DMPC/dehydration/EXP-Dvinskikh2005b_COUPLINGS.dat" u ($1):($2) every :::4::4 w lp ls 2 t "Dvinskikh"

unset multiplot
