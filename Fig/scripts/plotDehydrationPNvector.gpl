set term pdfcairo enhanced rounded color dashed dashlength 1 size 16.2cm, 10cm font ",18"
set out OUTPUTFILE

w=10 # linewidth simulation
r=2  # point radius simulation

set tmargin 0.5
set bmargin 3.2
set rmargin 1
set lmargin 9

set style line 15 lt 1 pt 11 ps r lw w lc rgb "#D70039" # for Berger DLPC
set style line 11 lt 2 pt 7 ps r lw w lc rgb "#D70039" # for Berger POPC
set style line 12 lt 1 pt 7 ps r lw w lc rgb "#0088BF" # for CHARMM36
set style line 13 lt 2 pt 7 ps r lw w lc rgb "#FFD300" # for GAFF
set style line 14 lt 1 pt 7 ps r lw w lc rgb "#00A368" # for MacRog

set key l at graph 0.05,0.95 Left reverse samplen 5 spacing 1.5 width 2 maxrows 5 
set xlabel "Hydration level (n_{w/l})" 
set mxtics 2
set ylabel "Deviation of P-N vector from \nmembrane normal (degrees)" offset +1

plot [60:0][65:105] \
     DATA_BERGER_DLPC u ($1):($2) ev 2 w lp ls 15 \
     t " DLPC 300K Berger", \
     DATA_BERGER u ($1):($2) w lp ls 11 \
     t " POPC 298K &{Be}@{\"}&{rger}", \
     DATA_MACROG u ($1):($2) w lp ls 14 \
     t " &{PO}@{\"}&{PC} 310K MacRog", \
     DATA_CHARMM36 u ($1):($2) w lp ls 12 \
     t " &{PO}@{\"}&{PC} 303K CHARMM36", \
     DATA_GAFF u ($1):($2) w lp ls 13 \
     t " &{PO}@{\"}&{PC} &{30}@{\"}&{3K} GAFFlipid"

